# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "updatehub-freescale"
BBFILE_PATTERN_updatehub-freescale = "^${LAYERDIR}/"
BBFILE_PRIORITY_updatehub-freescale = "6"

LAYERSERIES_COMPAT_updatehub-freescale = "hardknott"
LAYERDEPENDS_updatehub-freescale = "updatehub"


###
# Global setting to use UpadateHub with meta-freescale layer
#
# Setting to use wic image
IMAGE_BOOT_FILES_updatehub-imx ??= ""
WKS_FILE_DEPENDS_updatehub-imx += "virtual/bootloader"
IMAGE_FSTYPES_updatehub-imx ??= "tar.xz wic.bmap wic.gz"
WKS_SEARCH_PATH_updatehub-imx ??= "${THISDIR}:${@':'.join('%s/wic' % p for p in '${BBPATH}'.split(':'))}:${@':'.join('%s/scripts/lib/wic/canned-wks' % l for l in '${BBPATH}:${COREBASE}'.split(':'))}"

# UpdateHub settings
UPDATEHUB_ACTIVE_INACTIVE_BACKEND_updatehub-imx ??= "u-boot"
UPDATEHUB_DEVICE_IDENTITY_updatehub-imx ??= "primary-iface"
UPDATEHUB_FILESYSTEM_SUPPORT_updatehub-imx ??= "ext4"
UPDATEHUB_IMAGE_TYPE_updatehub-imx ??= "active/inactive"
UPDATEHUB_INSTALL_MODE_updatehub-imx ??= "tarball"

PREFERRED_PROVIDER_virtual/bootloader_updatehub-imx ??= "u-boot-fslc"
PREFERRED_RPROVIDER_u-boot-fw-utils_updatehub-imx ??= "libubootenv"
PREFERRED_PROVIDER_u-boot-default-script_updatehub-imx ??= "u-boot-updatehub-script"
UPDATEHUB_RUNTIME_PACKAGES_append_updatehub-imx = " u-boot-default-script u-boot-default-env"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS_updatehub-imx += " \
    kernel-image \
    kernel-devicetree \
"

# Do not change fstab file
WIC_CREATE_EXTRA_ARGS_updatehub-imx ??= "--no-fstab-update"

###
# Configuration for i.MX6 SABRE AUTO and SABRE Smart Device
#
# UpdateHub settings for imx6qdlsabresd machine
MACHINEOVERRIDES_prepend_imx6qdlsabresd = "updatehub-imx:"
WKS_FILES_updatehub-imx_imx6qdlsabresd ??= "updatehub.imx-uboot-spl-bootpart.wks.in"
IMAGE_BOOT_FILES_updatehub-imx_imx6qdlsabresd ??= "boot.scr-${MACHINE};boot.scr"
# UpdateHub Bootscript variables for imx6qdlsabresd machine
UPDATEHUB_BOOTSCRIPT_LOAD_A_updatehub-imx_imx6qdlsabresd ??= "\
load mmc \${mmcdev}:2 \${loadaddr} /boot/\${image}; run findfdt; run video_args_script; \
load mmc \${mmcdev}:2 \${fdt_addr} /boot/\${fdtfile}; \
"
UPDATEHUB_BOOTSCRIPT_LOAD_B_updatehub-imx_imx6qdlsabresd ??= "\
load mmc \${mmcdev}:3 \${loadaddr} /boot/\${image}; run findfdt; run video_args_script; \
load mmc \${mmcdev}:3 \${fdt_addr} /boot/\${fdtfile}; \
"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_A_updatehub-imx_imx6qdlsabresd ??= "part uuid mmc \${mmcdev}:2 uuid"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_B_updatehub-imx_imx6qdlsabresd ??= "part uuid mmc \${mmcdev}:3 uuid"
UPDATEHUB_BOOTSCRIPT_BOOTARGS_updatehub-imx_imx6qdlsabresd ??= "\
root=PARTUUID=\${uuid} rootfstype=ext4 rootwait rw \
console=\${console},\${baudrate} \${video_args} \
"
UPDATEHUB_BOOTSCRIPT_BOOTCMD_updatehub-imx_imx6qdlsabresd ??= "bootz \${loadaddr} - \${fdt_addr}"



###
# Configuration for i.MX6 Wandboard Quad/Dual/Solo
#
# UpdateHub settings for wandboard machine
MACHINEOVERRIDES_prepend_wandboard = "updatehub-imx:"
WKS_FILES_updatehub-imx_wandboard ??= "updatehub.imx-uboot-spl-bootpart.wks.in"
IMAGE_BOOT_FILES_updatehub-imx_wandboard ??= "boot.scr-${MACHINE};boot.scr"
# UpdateHub Bootscript variables for wandboard machine
UPDATEHUB_BOOTSCRIPT_LOAD_A_updatehub-imx_wandboard ??= "\
load mmc \${devnum}:2 \${loadaddr} /boot/${KERNEL_IMAGETYPE}; run findfdt; \
load mmc \${devnum}:2 \${fdt_addr} /boot/\${fdtfile} \
"
UPDATEHUB_BOOTSCRIPT_LOAD_B_updatehub-imx_wandboard ??= "\
load mmc \${devnum}:3 \${loadaddr} /boot/${KERNEL_IMAGETYPE}; run findfdt; \
load mmc \${devnum}:3 \${fdt_addr} /boot/\${fdtfile} \
"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_A_updatehub-imx_wandboard ??= "part uuid mmc \${devnum}:2 uuid"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_B_updatehub-imx_wandboard ??= "part uuid mmc \${devnum}:3 uuid"
UPDATEHUB_BOOTSCRIPT_BOOTARGS_updatehub-imx_wandboard ??= "\
console=\${console},\${baudrate} root=PARTUUID=\${uuid} rootwait rw \${video_args} \
"
UPDATEHUB_BOOTSCRIPT_BOOTCMD_updatehub-imx_wandboard ??= "bootz \${loadaddr} - \${fdt_addr}"



###
# Configuration for Boundary Devices
#
# UpdateHub settings for nitrogen6x machine
MACHINEOVERRIDES_prepend_nitrogen6x = "updatehub-imx:"
WKS_FILES_updatehub-imx_nitrogen6x ??= "updatehub.imx.wks"
PREFERRED_PROVIDER_virtual/bootloader_updatehub-imx_nitrogen6x ??= "u-boot-boundary"
PREFERRED_RPROVIDER_u-boot-fw-utils_updatehub-imx_nitrogen6x ??= "u-boot-boundary-fw-utils"
WKS_FILE_DEPENDS_updatehub-imx_nitrogen6x ??= "u-boot-script-boundary-updatehub"



###
# Configuration for Toradex machines
#
# UpdateHub settings for apalis-imx6 machine
MACHINEOVERRIDES_prepend_apalis-imx6 = "updatehub-imx:"
WKS_FILES_updatehub-imx_apalis-imx6 ??= "updatehub.imx.wks"
UPDATEHUB_RUNTIME_PACKAGES_append_apalis-imx6 = " updatehub-config-toradex"

# UpdateHub settings for colibri-imx6 machine
MACHINEOVERRIDES_prepend_colibri-imx6 = "updatehub-imx:"
WKS_FILES_updatehub-imx_colibri-imx6 ??= "updatehub.imx.wks"
UPDATEHUB_RUNTIME_PACKAGES_append_colibri-imx6 = " updatehub-config-toradex"

# UpdateHub settings for colibri-imx7-emmc machine
MACHINEOVERRIDES_prepend_colibri-imx7-emmc = "updatehub-imx:"
WKS_FILES_updatehub-imx_colibri-imx7-emmc ??= "updatehub.imx.wks"
UPDATEHUB_RUNTIME_PACKAGES_append_colibri-imx7-emmc = " updatehub-config-toradex"



###
# Configuration for TechNexion/NXP devices
#
MACHINEOVERRIDES_prepend_imx7d-pico = "updatehub-imx:"
WKS_FILES_updatehub-imx_imx7d-pico ??= "updatehub.imx-uboot-spl-bootpart.wks.in"
IMAGE_BOOT_FILES_updatehub-imx_imx7d-pico ??= "boot.scr-${MACHINE};boot.scr"
# UpdateHub Bootscript variables for imx7d-pico machine
UPDATEHUB_BOOTSCRIPT_LOAD_A_updatehub-imx_imx7d-pico ??= "\
load mmc \${mmcdev}:2 \${loadaddr} /boot/\${image}; run findfdt; run video_args_script; \
load mmc \${mmcdev}:2 \${fdt_addr} /boot/\${fdtfile}; \
"
UPDATEHUB_BOOTSCRIPT_LOAD_B_updatehub-imx_imx7d-pico ??= "\
load mmc \${mmcdev}:3 \${loadaddr} /boot/\${image}; run findfdt; run video_args_script; \
load mmc \${mmcdev}:3 \${fdt_addr} /boot/\${fdtfile}; \
"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_A_updatehub-imx_imx7d-pico ??= "part uuid mmc \${mmcdev}:2 uuid"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_B_updatehub-imx_imx7d-pico ??= "part uuid mmc \${mmcdev}:3 uuid"
UPDATEHUB_BOOTSCRIPT_BOOTARGS_updatehub-imx_imx7d-pico ??= "\
root=PARTUUID=\${uuid} rootfstype=ext4 rootwait rw \
console=\${console},\${baudrate} \${video_args} \
"
UPDATEHUB_BOOTSCRIPT_BOOTCMD_updatehub-imx_imx7d-pico ??= "bootz \${loadaddr} - \${fdt_addr}"
